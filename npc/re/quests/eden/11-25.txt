//===== rAthena Script =======================================
//= Eden Group Quests - Mission [11 - 25]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 11 - 25.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Bug fixes. [Capuche]
//= 1.4 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.5 Added use of F_HasEdenGroupMark function. [Ragno]
//= 1.6 Standardise board messages with higher level boards
//=     for ChocobotRO [Latiosu]
//============================================================

moc_para01,36,38,3	script	Mission [11 - 25]#Tuto	4_BOARD3,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "You are not an Eden group member. You are not qualified to access the bulletin board.";
		close;
	}
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 1000) {
		mes "^008800Wait a sec!!";
		mes "You cannot receive any more items because you're carrying too many items. Please try again after you lighten up.^000000";
		close;
	}
	mes "[Mission Board]";
	mes "Mission bulletin board [Lv 11 - 25].";
	next;
	.@choice = select("Check the Missions.:Turn in Missions.:Cancel.");
	if (.@choice == 1) {
		if ((BaseLevel < 11) || (BaseLevel > 25)) {
			mes "These missions are only for Lv 11 - 25 adventurers.";
			close;
		}
		switch(select("Hornet Hunting:Condor Hunting:Grasshopper's Leg:Worm Tail Hunting:Spore Hunting:Pest Control:Muka Hunting:Familiar Hunting:Collect Feathers:Collect Poison Spores")) {
			case 1: callsub L_Quest,11114,"Hornet Hunting","Hunt 10 Hornets","Northwest of Prontera.";
			case 2: callsub L_Quest,11115,"Condor Hunting","Hunt 20 Condors","South of Morocc.";
			case 3: callsub L_Quest,11116,"Grasshopper's Leg","Hunt 10 Rockers and bring 10 Grasshopper's Legs.","Southwest of Prontera";
			case 4: callsub L_Quest,11117,"Worm Tail Hunting","Hunt 20 Worm Tails","Southeast of Payon.";
			case 5: callsub L_Quest,11118,"Spore Hunting","Hunt 30 Spores","Around Payon.";
			case 6: callsub L_Quest,11119,"Pest Control","Hunt 20 Thief Bug Eggs and bring 10 Chrysalis.","In the Prontera Culverts, West of Prontera.";
			case 7: callsub L_Quest,11120,"Muka Hunting","Hunt 20 Muka","South of Prontera.";
			case 8: callsub L_Quest,11121,"Familiar Hunting","Hunt 20 Familiar","In the Prontera Culverts, West of Prontera.";
			case 9: callsub L_Quest,11122,"Collect Feathers","Hunt 30 Peco Pecos","South of Prontera.";
			case 10: callsub L_Quest,11123,"Collect Poison Spores","Hunt 30 Poison Spores and bring 5 Poison Spores.","Inside the Geffen Tower";
		}
		end;
	} else if (.@choice == 2) {
		setarray .@Exp[0],900,1800,1400,2600,3900,2900,3200,3200,5400,6000;
		setarray .@Item[0],0,0,940,0,0,915,0,0,0,7033;
		setarray .@Amount[0],0,0,10,0,0,10,0,0,0,5;
		for (set .@i,0; .@i<10; set .@i,.@i+1) {
			if (checkquest(11114+.@i,HUNTING) == 2) {
				if (.@Item[.@i]) {
					if (countitem(.@Item[.@i]) < .@Amount[.@i]) {
						mes "[Mission Board]";
						mes "You don't have enough "+ getitemname(.@Item[.@i]) +".";
						mes "You need to bring "+ .@Amount[.@i] +" "+ getitemname(.@Item[.@i]) +".";
						close;
					}
				}
				mes "[Mission Board]";
				mes "You have completed the mission.";
				mes "Here is your reward.";
				if (.@Item[.@i]) {
					delitem .@Item[.@i],.@Amount[.@i];
				}
				erasequest 11114+.@i;
				specialeffect2 EF_STEAL;
				getexp .@Exp[.@i],.@Exp[.@i]/2;
				close;
			}
		}
		mes "[Mission Board]";
		mes "No missions are ready to turn in yet.";
		close;
	} else {
		mes "^000077Ok. Let's check the missions next time.^000000.";
		close;
	}
L_Quest:
	if (checkquest(getarg(0),HUNTING) >= 0) {
		mes "[Mission Board Memo]";
		mes "You have already accepted this mission.";
		close;
	}
	mes "["+ getarg(1) +" !!]";
	mes "^0000FFLocation:^000000 "+getarg(2);
	mes "^0000FFMission:^000000 "+getarg(3);
	next;
	if(select("Accept the mission.:Cancel.") == 1) {
		mes "[Mission Board]";
		mes "You have accepted the mission. For more details, please press 'Alt + U' keys, you can get information from the Quest Window.";
		setquest getarg(0);
	}
	close;
}
